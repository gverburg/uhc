%%[doesWhat doclatex

Eliminate Grin \textit{App} and \textit{Eval} expressions 
by inlining equivalent code, based on full-program `PointsTo' analysis.

Requires FlattenSeq to be the next transformation.
Inspects the HPT table and updates it for introduced fresh variables.

%%]

     EVAL locat
==>
     FETCHNODE locat ; \node ->
     CASE node OF
     { ident   (#C/c a b) -> UNIT node                     -- for every final tag in HptMap of locat
     ; calling (#F/f a b) -> CALL f a b ; \result  ->      -- for every F- and A-tag in HptMap of locat
                             (UPDATE)UNIT result locat     -- no update needed in unique context
     }


     APP node p q
==>
     CASE node OF
     { normal (#P/n/f x y) -> UNIT (#P/m/f x y p q)       -- undersaturated:    n > |p q|  (function needs more arguments than given), m = n - |p q|
     ; normal (#P/n/f x y) -> CALL f x y p q              -- exactly saturated: n = |p q|
     ; normal (#P/n/f x y) -> CALL f x y p   ;\result ->  -- oversaturated      n < |p q|  (too many arguments given for needs), n = |p|
                              CASE result OF ...q...      -- recurse
     }


%%[(8 codegen grin) hs module {%{EH}GrinCode.Trf.InlineEA} export(inlineEA)
%%]

%%[(8 codegen grin) ag import({GrinCode/AbsSyn})
%%]
%%[(8 codegen grin).wrapper
WRAPPER GrAGItf
%%]
%%[(8 codegen grin) hs import(Data.List, Data.Maybe, qualified Data.Set as Set, qualified Data.Map as Map )
%%]
%%[(8 codegen grin) hs import({%{EH}Base.Common}, {%{EH}GrinCode})
%%]
%%[(8 codegen grin) hs import({%{EH}GrinCode.Common(HptMap, getBaseEnvList, getNr, absFetch, getEnvVar, getNodes, addEnvElems, getEnvSize, throwTag, AbstractValue(..))})
%%]
%%[(8 codegen grin) hs import(Debug.Trace)
%%]

%%[(8 codegen grin) hs
inlineEA :: Bool -> (GrModule,HptMap) -> (GrModule,HptMap)
inlineEA doUsageAnalysis (code,hptMap)
  = let inh = Inh_GrAGItf { hptMap_Inh_GrAGItf=hptMap
                          , doUsageAnalysis_Inh_GrAGItf=doUsageAnalysis
                          }
        syn = wrap_GrAGItf (sem_GrAGItf (GrAGItf_AGItf code)) inh
        grTrf  = grTrf_Syn_GrAGItf syn
        hptMap2 = hptMap_Syn_GrAGItf syn
    in  ( grTrf, hptMap2)

%%]


%%[(8 codegen grin) hs

type HptItems       = [ (Int,AbstractValue) ]


getLocs :: AbstractValue -> [Int]
getLocs AbsBottom = []
getLocs (AbsLocs xs _) = Set.toList xs

maximum0 [] = 0
maximum0 xs = maximum xs

unevaluatedName :: GrTag -> Maybe HsName
unevaluatedName (GrTag_Fun nm) = Just nm
unevaluatedName (GrTag_App nm) = Just nm
unevaluatedName _              = Nothing

isPartialApplication ((GrTag_PApp _ _), _) = True
isPartialApplication _ = False

isEmptyAbsVal AbsBottom      = True
isEmptyAbsVal (AbsLocs xs m) = Set.null xs
isEmptyAbsVal v              = error $ "wrong content of exception variable of binding " ++ show v


makeCaseExpression
  :: ([GrAlt] -> GrExpr)                                                    ->   -- given a function to build an Expr from a [Alt]
     (Int -> HptMap ->  (GrTag,[AbstractValue])  -> (Int,HptItems, GrAlt )) ->   -- and   a function to build an Alt from a Node (updating the unique and generating new HptItems)
      Int -> HptMap -> [(GrTag,[AbstractValue])] -> (Int,HptItems, GrExpr)       -- we can construct a function to build a (Case-)Expr from a [Node] (updating the unique and generating new HptItems)

makeCaseExpression buildCase buildAlt unique hptMap nodes
  =  let makeAlt node (uniqPrev,             itemsPrev,        altsPrev) 
                = let (uniqNext, itemsNext,            altNext         ) = buildAlt uniqPrev hptMap node
                  in  (uniqNext, itemsNext++itemsPrev, altNext:altsPrev)

         (unique2, items, alts) = foldr makeAlt (unique,[],[]) nodes
     in  (unique2, items, buildCase alts)

newName :: Int -> HptMap -> AbstractValue -> (Int,HptItems,HsName)
newName unique hptMap av
  =  let name = HNmNr unique OrigNone
         unique2 = unique+1
         entries = [(unique,av)]
     in (unique2, entries, name)

newNames :: Int -> HptMap -> [AbstractValue] -> (Int,HptItems,[HsName])
newNames unique hptMap avs
  =  let entries = zip [unique..] avs
         names   = map (\e -> HNmNr (fst e) OrigNone) entries
         unique2 = unique + length avs
     in (unique2, entries, names)

%%]

%%[(8 codegen grin)

ATTR AllNT     [ | | grTrf: SELF     ]
ATTR GrAGItf   [ | | grTrf: GrModule ]

ATTR AllGrExpr
     AllBind
     GrModule  [ | | newItems USE {++} {[]} : HptItems ]
ATTR GrAGItf   [ | | hptMap: HptMap ]

ATTR GrAGItf 
     GrModule
     AllBind
     AllGrExpr [ hptMap: HptMap doUsageAnalysis : Bool |  | ]

ATTR GrModule
     AllBind
     AllGrExpr [ | unique : Int | ]

SEM GrAGItf
  | AGItf      module . unique  =  getEnvSize @lhs.hptMap
               lhs    . hptMap  =  addEnvElems @lhs.hptMap (map snd (sort @module.newItems))


SEM GrExpr
  | Eval  loc.(unique, newItems, grTrf)  =  evalCode  @lhs.unique @lhs.hptMap @nm (if @lhs.doUsageAnalysis then (Map.findWithDefault True (getNr @nm) @lhs.needsUpdate) else True)
  | App   loc.(unique, newItems, grTrf)  =  applyCode @lhs.unique @lhs.hptMap @nm (getEnvVar @lhs.hptMap (getNr @nm)) @argL.grTrf

%%]





%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Eval code generation %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%[(8 codegen grin) hs


buildEvalAlt
 :: HsName                       ->      -- locatName
    Bool                         ->      -- locatNeedsUpdate
    HsName                       ->      -- nodeName
    Int                          ->      -- unique
    HptMap                       ->      -- hptMap
    ( GrTag                              -- tag
    , [AbstractValue]                    -- args
    )                            ->
    ( Int                                -- updated unique
    , HptItems                           -- newItems
    , GrAlt                              -- generated code
    )

buildEvalAlt locatName locatNeedsUpdate nodeName unique hptMap (tag,args)
 =  let (unique2, newItems2, patNames) = newNames unique hptMap args
        codeHead annot = GrAlt_Alt annot (GrPatAlt_Node tag patNames)
    in  case unevaluatedName tag of
          Nothing -> (unique2, newItems2, codeHead GrAltAnnIdent (GrExpr_Unit (GrVal_Var nodeName)))
          Just functName  ->
                   let -- get the abstract value for the function-result and exception-result of the called function
                       resultAbsVal= getEnvVar hptMap (getNr functName)
                       exceptAbsVal= getEnvVar hptMap (getNr functName + 1)
                       -- create new name for the call result
                       (unique3, newItems3, resultName) = newName unique2 hptMap resultAbsVal
                       resultMaxArity = maximum0 (map (length . snd) (getNodes resultAbsVal))
                       -- wrap the newly created names for use in Grin-values and -patterns
                       resultVal  = GrVal_Var resultName
                       resultPat  = GrPatLam_Var resultName
                       emptyPat   = GrPatLam_Empty
                       -- generate code for the call
                       callExpr0   = GrExpr_Call functName $ map GrVal_Var patNames
                       callExpr1   = GrExpr_Seq callExpr0 resultPat  $
                                     GrExpr_Unit resultVal
                       callExpr2   = GrExpr_Seq callExpr0 resultPat  $
                                     GrExpr_UpdateUnit locatName resultVal
                       callExpr    = if   locatNeedsUpdate
                                     then callExpr2
                                     else callExpr1
                   in  if  isEmptyAbsVal exceptAbsVal
                        then (unique3, newItems2++newItems3, codeHead (GrAltAnnCalling resultMaxArity locatName) callExpr )
                        else -- handling exceptions below (currently not used)
                             let -- create some more new names
                                 (unique4, newItems4, [throwVar,catchVar]) = newNames unique3 hptMap [exceptAbsVal, resultAbsVal]
                                 noVar      = HNmNr 0 OrigNone
                                 noPat      = GrPatLam_Var noVar
                                 -- and wrap them for use in Grin
                                 throwVal   = GrVal_Var throwVar
                                 catchVal   = GrVal_Var catchVar
                                 catchPat   = GrPatLam_Var catchVar
                                 -- create a node for the exception
                                 eNode      = GrVal_Node throwTag [throwVal]
                                 -- generate code for handling the exception
                                 eCode      = GrExpr_Catch callExpr throwVar                        $
                                              GrExpr_Seq (GrExpr_UpdateUnit locatName eNode) noPat  $
                                              GrExpr_Throw throwVar
                                 handleExpr = GrExpr_Seq eCode catchPat $
                                              GrExpr_Unit catchVal
                             in  (unique4, newItems2++newItems3++newItems4, codeHead (GrAltAnnCalling resultMaxArity locatName) handleExpr )



evalCode
 :: Int                          ->      -- unique
    HptMap                       ->      -- hptMap
    HsName                       ->      -- locatName
    Bool                         ->      -- locatNeedsUpdate
    ( Int                                -- updated unique
    , HptItems                           -- newItems
    , GrExpr                             -- generated code
    )

evalCode unique hptMap locatName locatNeedsUpdate
 = let -- get the abstract value for the original location
       nodeAbsVal    = absFetch hptMap locatName
       -- create new name for the fetched node
       (unique2, newItems2, nodeName) = newName unique hptMap nodeAbsVal
       -- wrap the newly created name for use in Grin-patterns and -values
       nodePat       = GrPatLam_Var nodeName
       nodeVal       = GrVal_Var    nodeName
       -- we must generate code for all possible nodes, as found in the abstract value
       nodes         = getNodes nodeAbsVal
       codeHead      = GrExpr_Seq (GrExpr_FetchNode locatName) nodePat . GrExpr_Case nodeVal
       
       (unique3, newItems3, expr) = makeCaseExpression codeHead (buildEvalAlt locatName locatNeedsUpdate nodeName) unique2 hptMap nodes
   in  (unique3, newItems2++newItems3, expr)

%%]


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Apply code generation %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%[(8 codegen grin) hs

buildApplyAlt
 :: 
    [GrVal]                      ->      -- appArgs
    Int                          ->      -- unique
    HptMap                       ->      -- hptMap
    ( GrTag                              -- tag
    , [AbstractValue]                    -- args
    )                            ->
    ( Int                                -- updated unique
    , HptItems                           -- newItems
    , GrAlt                              -- generated code
    )

buildApplyAlt appArgs unique hptMap (tag@(GrTag_PApp needsParams functName), args)
 = 
    let hasParams    = length args
        getsParams   = length appArgs
        (unique2, newItems2, patNames) = newNames unique hptMap args
        patVars      = map GrVal_Var patNames
        codeHead     = GrAlt_Alt GrAltAnnNormal (GrPatAlt_Node tag patNames)
    in  case compare getsParams needsParams of
          LT -> -- undersaturated
                let newtag = GrTag_PApp (needsParams-getsParams) functName 
                in  (unique2, newItems2, codeHead $ GrExpr_Unit (GrVal_Node newtag (patVars ++ appArgs)))
          EQ -> -- exactly saturated
                    (unique2, newItems2, codeHead $ GrExpr_Call functName          (patVars ++ appArgs))
          GT -> -- oversaturated
                let -- get the abstract value for the function-result of the called function
                    resultAbsVal= getEnvVar hptMap (getNr functName)
                    -- create a new variable to accept the result of the function call
                    (unique3, newItems3, resultName) = newName unique2 hptMap resultAbsVal
                    resultPat  = GrPatLam_Var resultName
                    -- generate code for the function call
                    code1 = GrExpr_Call functName (patVars ++ take needsParams appArgs)
                    -- generate code for recursing on the remaining arguments
                    (unique4, newItems4, code2)  = applyCode unique3 hptMap resultName resultAbsVal (drop needsParams appArgs)
                in  -- sequence the function call and the processing of remaining arguments
                    (unique4, newItems2++newItems3++newItems4, codeHead $ GrExpr_Seq code1 resultPat code2 )



applyCode
 :: Int                          ->      -- unique
    HptMap                       ->      -- hptMap
    HsName                       ->      -- nodeName
    AbstractValue                ->      -- nodeAbsVal
    [GrVal]                      ->      -- appArgs
    ( Int                                -- updated unique
    , HptItems                           -- newItems
    , GrExpr                             -- generated code
    )

applyCode unique hptMap nodeName nodeAbsVal appArgs
 = let -- get the abstract value for the function to be applied
       -- nodeAbsVal = getEnvVar hptMap (getNr nodeName)
       nodeVal    = GrVal_Var nodeName
       -- we must generate code for all possible nodes, as found in the abstract value
       nodes = filter isPartialApplication (getNodes nodeAbsVal)
   in  makeCaseExpression (GrExpr_Case nodeVal) (buildApplyAlt appArgs) unique hptMap nodes

%%]


%%[(8 codegen grin) hs
mapUnionWithPlus = Map.unionWith (+)
mapUnionWithCat  = Map.unionWith (++)
%%]



%%[(8 codegen grin)

ATTR AllBind
     AllGrExpr     [ needsUpdate : {Map.Map Int Bool} 
                   | 
                   | 
                   ]


ATTR AllBind
     AllGrExpr
     AllGrVal      [ | | textualUses       USE {`mapUnionWithPlus`} {Map.empty} :  {Map.Map Int  Int } 
                         possibleArguments USE {`mapUnionWithCat` } {Map.empty} :  {Map.Map Int [Int]} 
                   ]

ATTR AllGlobal     [ | | updateNeededList  USE {++} {[]} : {[(Int,Bool)]}     ]     
ATTR GrTag
     GrVal         [ | | updateNeeded : {Bool} ]
     

SEM GrVal
  | Var          lhs.textualUses = Map.singleton (getNr @nm) 1

SEM GrExpr
  | FetchNode 
    FetchField   lhs.textualUses = Map.singleton (getNr @nm ) 1
  | FetchUpdate  lhs.textualUses = Map.singleton (getNr @src) 1
  | Eval         lhs.textualUses = Map.singleton (getNr @nm ) 1
  | App          lhs.textualUses = mapUnionWithPlus @argL.textualUses (Map.singleton (getNr @nm) 1)

SEM GrVal
  | Node         lhs.possibleArguments = maybe (Map.empty)
                                               (\n -> Map.fromList (zip [n+2..] @fldL.varLists))
                                               @tag.mbFunNr


{
possibleArgumentsOfApplication hptMap nm vl = 
    let nodeAbsVal = getEnvVar hptMap (getNr nm)
        nodes = filter isPartialApplication (getNodes nodeAbsVal)
        ms = [  (let  nvl = length vl
                      vl1 = take needs vl
                      vl2 = drop needs vl
                      first = Map.fromList (zip [getNr name+2+length args..] vl1)
                 in   (if   nvl<=needs
                       then first
                       else Map.unionWith (++) first (possibleArgumentsOfApplication hptMap name vl2)
                      )
                )
             | (GrTag_PApp needs name, args) <- nodes 
             ]
    in  Map.unionsWith (++) ms
}


SEM GrExpr
  | Call         lhs.possibleArguments = let n = getNr @nm
                                         in  Map.fromList (zip [n+2..] @argL.varLists)
  | App          lhs.possibleArguments = possibleArgumentsOfApplication @lhs.hptMap @nm @argL.varLists

SEM GrGlobal
  | Global       lhs.updateNeededList  =  [(getNr @nm, @val.updateNeeded)]



ATTR GrTag   [ | | mbFunNr : {Maybe Int} ]

SEM GrTag
  | Fun
    App
    PApp       lhs.mbFunNr = Just (getNr @nm)
  | * - Fun
        App
        PApp   lhs.mbFunNr = Nothing

SEM GrTag
  | Fun
    App        lhs.updateNeeded = True
  | * - Fun
        App    lhs.updateNeeded = False

SEM GrVal
  | Node       lhs.updateNeeded  =  @tag.updateNeeded
  | *-Node     lhs.updateNeeded  =  False

        
ATTR GrValL [ | | varLists : {[[Int]]} ]

SEM GrValL
  | Nil     lhs.varLists = []
  | Cons    lhs.varLists = @hd.varList : @tl.varLists
  
ATTR GrVal [ | | varList : {[Int]} ]

SEM GrVal
  | Var     lhs.varList  = [ getNr @nm ]
  | *-Var   lhs.varList  = []


SEM GrModule
  | Mod     bindL.needsUpdate  =  let updatableLocationsSet = Set.fromList [ head (getLocs (getEnvVar @lhs.hptMap x))
                                                                           | (x,True) <- @globalL.updateNeededList
                                                                           ]
            
                                      globalUpdateNeededMap =  Map.fromList @globalL.updateNeededList
                                      multipleUsageMap      =  Map.map (>1) @bindL.textualUses
                                      holdsUpdatableLocMap  =  Map.fromList [ (x,True)
                                                                            | (x, AbsLocs ls _) <- getBaseEnvList @lhs.hptMap
                                                                            , not (Set.null (Set.intersection ls updatableLocationsSet))
                                                                            ]
            
                                      un1 = Map.union globalUpdateNeededMap (Map.union holdsUpdatableLocMap multipleUsageMap)
                                                      -- exploits left-biased union: for globals, the numer of uses is not important, only whether they are bound to an F/A-tag or not.

                                      pa = @bindL.possibleArguments
                                      step un = let f n b = maybe b
                                                                  (or . (b:) . (map (\a -> Map.findWithDefault False a un)))
                                                                  (Map.lookup n pa)
                                                in Map.mapWithKey f un
        
                                      uns = iterate step un1
                                      (aantal, un99) = fixp 0 uns
                                  in  un99  -- trace ("un1: " ++ show un1 ++ "pa: " ++ show pa ++ "un99: " ++ show un99) un99
   
{
fixp d (x:y:zs)  |  Map.toAscList x == Map.toAscList y  =  (d,x)
                 |  otherwise                           =  fixp (d+1) (y:zs)
}
   
%%]







%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Unsuccesfull attempt below


ATTR AllBind
     AllGrExpr     [ | | evalPoints USE {++} {[]} : {[HsName]} ] 

SEM GrExpr
  | Eval   lhs.evalPoints = [@nm]


ATTR AllBind
     AllGrExpr     [ useCountMap : {Map.Map Int Int} | | ]
     
SEM GrModule
  | Mod  bindL.useCountMap = let eps =  @bindL.evalPoints
                                 ols =  origLocs @lhs.hptMap eps
                                 lfs =  locFreq ols
                                 mfs =  maxFreq ols lfs
                                 mmp =  makeMaxMap mfs
                             in  trace ("origlocs: " ++ show ols ++ "locFreqs: " ++ show lfs ++ "maxFreqs: " ++ show mfs ++ "maxMap: " ++ show mmp) 
                                        mmp


{

origLocs :: HptMap -> [HsName] -> [(HsName, [Int])]
origLocs hptMap xs
  =  [ (x, getLocs (getEnvVar hptMap (getNr x)))
     | x <- xs
     ]

getLocs :: AbstractValue -> [Int]
getLocs AbsBottom = []
getLocs (AbsLocs xs _) = Set.toList xs

locFreq :: [(HsName, [Int])] -> Map.Map Int Int
locFreq ts
 = foldr count Map.empty (concat (map snd ts))

count :: Int -> Map.Map Int Int -> Map.Map Int Int
count x m = Map.insertWith (+) x 1 m

maxFreq :: [(HsName, [Int])] -> Map.Map Int Int -> [(HsName, Int)]
maxFreq ts m
 = [ (name, maximum0 fs)
   | (name, locs) <- ts
   , let fs = map (\loc -> Map.findWithDefault 0 loc m) locs
   ]

makeMaxMap :: [(HsName,Int)] -> Map.Map Int Int
makeMaxMap ts
 = foldr ins Map.empty ts
 
ins :: (HsName,Int) -> Map.Map Int Int -> Map.Map Int Int
ins (name,freq) m = Map.insert (getNr name) freq m


}
